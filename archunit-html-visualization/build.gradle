plugins {
    id "com.moowork.node" version "1.2.0"
}

ext.moduleName = 'com.tngtech.archunit.htmlvisualization'

dependencies {
    compile project(path: ':archunit', configuration: 'shadow')
    compile dependency.guava
    compile dependency.gson
    compile dependency.slf4j

    testCompile dependency.log4j_api
    testCompile dependency.log4j_core
    testCompile dependency.log4j_slf4j
    testCompile dependency.junit4
    testCompile dependency.mockito
    testCompile dependency.assertj
    testCompile(dependency.assertj_guava) {
        exclude module: 'assertj-core'
        exclude module: 'guava'
    }
    testCompile dependency.json_assert
    testCompile dependency.junit_dataprovider
    testCompile project(path: ':archunit', configuration: 'tests')
}

shadowJar {
    exclude 'META-INF/maven/**'
}

configurations {
    /* Exclude archunit from the shadow jar */
    runtime.exclude module: ':archunit'
}

def nodeVersion = '10.14.2'
def nodeDir = file("${project.projectDir}/nodejs")

node {
    version = nodeVersion
    distBaseUrl = 'https://nodejs.org/dist'
    download = true
    workDir = nodeDir
}

task setupNode(dependsOn: ['npmInstall']) {
    doLast {
        def subFolder = nodeDir.list().grep { it.contains(nodeVersion) }[0]
        println """
            >> Setting up NodeJs and running npm install...
            >> To run node from the command line, add the nodejs bin folder to your PATH:
            >> export PATH="${project.projectDir}/nodejs/${subFolder}/bin:\$PATH"
            """.stripIndent()
    }
}

task cleanNode(type: Delete) {
    delete 'nodejs', 'node_modules'
}

task cleanAll(dependsOn: [clean, cleanNode])

task mocha(type: NpmTask, dependsOn: 'setupNode') {
    args = ['run', 'test']
}

task eslint(type: NpmTask, dependsOn: 'setupNode') {
    args = ['run', 'lint']
}

def visualizationOutputDir = "${sourceSets.main.output.resourcesDir}/com/tngtech/archunit/htmlvisualization"

task createReportDir {
    doFirst {
        file(visualizationOutputDir).mkdirs()
    }
}

task webpack(type: NpmTask, dependsOn: ['mocha', 'eslint', 'setupNode', 'createReportDir']) {
    args = ['run', 'createJsBundle', '--', '--env.buildPath=' + visualizationOutputDir]
}

task bundleHtml(type: NpmTask, dependsOn: 'webpack') {
    args = ['run', 'bundleHtml',
            '--',
            '--redirect', 'build://|' + visualizationOutputDir + '/',
            '--out-file', visualizationOutputDir + '/report.html',
            'src/main/app/report.html']
}

task deleteJsBundles(type: Delete, dependsOn: 'bundleHtml') {
    delete fileTree(visualizationOutputDir) {
        include '**/*.js'
    }
}

task buildReport(dependsOn: ['mocha', 'eslint', 'webpack', 'bundleHtml', 'deleteJsBundles']) {}

test.dependsOn buildReport
assemble.dependsOn buildReport
