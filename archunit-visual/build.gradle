plugins {
    id "com.moowork.node" version "1.1.1"
}

dependencies {
    compile project(path: ':archunit', configuration: 'shadow')
    compile dependency.guava
    compile dependency.gson

    testCompile dependency.log4j_api
    testCompile dependency.log4j_core
    testCompile dependency.log4j_slf4j
    testCompile dependency.junit
    testCompile dependency.mockito
    testCompile dependency.assertj
    testCompile(dependency.assertj_guava) {
        exclude module: 'assertj-core'
        exclude module: 'guava'
    }
    testCompile dependency.junit_dataprovider
    testCompile project(path: ':archunit', configuration: 'tests')
}

/* We only depend on archunit core and nothing else */
install.repositories.mavenInstaller.pom.whenConfigured { pom ->
    pom.dependencies.removeAll {
        it.artifactId != 'archunit' || it.scope != 'compile'
    }
}

configurations {
    /* Exclude archunit from the shadow jar */
    runtime.exclude module: ':archunit'
}

def nodeVersion = '7.2.0'

node {
    version = nodeVersion
    npmVersion = '4.0.2'
    distBaseUrl = 'https://nodejs.org/dist'
    download = true
    workDir = file("${project.buildDir}/nodejs")
    nodeModulesDir = file("${project.projectDir}")
}

task installBrowserify(type: NpmTask) {
    args = ['install', 'browserify']
}

task installTestSupport(type: NpmTask) {
    args = ['install', 'mocha', 'chai', '--save-dev']
}

task setupNode(dependsOn: ['installBrowserify', 'installTestSupport']) {
    doLast {
        def subFolder = file("${project.buildDir}/nodejs").list().grep { it.contains(nodeVersion) }[0]
        println """
            >> Ensuring Browserify is installed...
            >> Ensuring Mocha and Chai exist...
            >> To run node from the command line, add the nodejs bin folder to your PATH:
            >> export PATH="${project.buildDir}/nodejs/${subFolder}/bin:\$PATH"
            """.stripIndent()
    }
}

task mocha(type: NodeTask, dependsOn: 'setupNode') {
    script = file('node_modules/mocha/bin/mocha')
    args = ['--reporter', 'spec', 'src/test/app/report/**/*.js']
}

def reportOutputDir = "${sourceSets.main.output.resourcesDir}/com/tngtech/archunit/visual/report"

task createReportDir {
    doFirst {
        file(reportOutputDir).mkdirs()
    }
}

task copyStatic(type: Copy, dependsOn: createReportDir) {
    from 'src/main/app/report'
    include '*.html', '*.css'
    into reportOutputDir
}

task copyJsLibs(type: Copy, dependsOn: createReportDir) {
    from 'src/main/app/report/lib'
    include 'd3.js'
    include 'jscolor.js'
    into "${reportOutputDir}/lib"
}

task browserifyReportTree(type: NodeTask, dependsOn: ['installBrowserify', 'createReportDir']) {
    script = file('node_modules/browserify/bin/cmd.js')
    args = ['-r', './src/main/app/report/tree.js:tree', '-o', "${reportOutputDir}/tree-bundle.js"]
}

task browserifyReportDep(type: NodeTask, dependsOn: ['installBrowserify', 'createReportDir']) {
    script = file('node_modules/browserify/bin/cmd.js')
    args = ['-r', './src/main/app/report/dependencies.js:dependencies', '-o', "${reportOutputDir}/dependencies-bundle.js"]
}

task browserify(dependsOn: ['browserifyReportTree', 'browserifyReportDep']) {}

task buildReport(dependsOn: ['copyStatic', 'copyJsLibs', 'mocha', 'browserify']) {}

jar.dependsOn buildReport