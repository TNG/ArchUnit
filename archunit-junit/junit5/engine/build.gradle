plugins {
    id 'archunit.java-release-conventions'
}

repositories {
    mavenLocal()
}

ext.moduleName = 'com.tngtech.archunit.junit5.engine'

ext.minimumJavaVersion = JavaVersion.VERSION_1_8

dependencies {
    api project(path: ':archunit')
    api project(path: ':archunit-junit5-api')
    api project(path: ':archunit-junit5-engine-api')
    implementation project(path: ':archunit-junit')
    dependency.addGuava { dependencyNotation, config -> implementation(dependencyNotation, config) }
    implementation dependency.slf4j
    implementation dependency.junit5JupiterApi
    implementation(group: 'org.apache.maven.surefire', name: 'surefire-junit-platform', version: '3.0.0-M6-SNAPSHOT') {
        artifact {
            classifier = 'api'
        }
    }

    testImplementation project(path: ':archunit', configuration: 'tests')
    testImplementation dependency.assertj
    testImplementation dependency.mockito

    testRuntimeOnly dependency.junit5JupiterEngine
}

gradle.projectsEvaluated {
    compileJava.dependsOn project(':archunit-junit5-api').finishArchive
    compileJava.dependsOn project(':archunit-junit5-engine-api').finishArchive
}

javadoc {
    source(project(':archunit-junit').sourceSets.main.allJava)
}
sourcesJar {
    from project(':archunit-junit').sourceSets.main.allSource
}

archUnitTest {
    hasSlowTests = true
}

test {
    useJUnitPlatform() {
        excludeEngines 'archunit'
    }
}

shadowJar {
    exclude 'META-INF/maven/**'

    dependencies {
        exclude(dependency {
            def isApi = it.configuration == 'archJunitApi'
            def isUnwantedDependency = it.name != dependency.guava && it.moduleName != 'archunit-junit'
            isUnwantedDependency || isApi
        })
    }
}

def configureDependencies = { deps ->
    deps.children().removeIf { dep ->
        dep.scope.text() != 'compile' || !(dep.artifactId.text() in ['archunit', 'archunit-junit5-api', 'archunit-junit5-engine-api'])
    }
}
this.with project(':archunit-junit').configureJUnitArchive(configureDependencies)
