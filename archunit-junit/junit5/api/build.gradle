plugins {
    id 'archunit.java-conventions'
}

ext.moduleName = 'com.tngtech.archunit.junit5.api'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    api project(path: ':archunit')
    api project(path: ':archunit-junit', configuration: 'archJunitApi')
    api dependency.junitPlatformCommons
}

javadoc {
    source(project(':archunit-junit').sourceSets.archJunitApi.allJava)
}
sourcesJar {
    from project(':archunit-junit').sourceSets.archJunitApi.allSource
}

shadowJar {
    exclude 'META-INF/maven/**'

    dependencies {
        exclude(project(':archunit'))
        exclude(dependency { it.name != dependency.guava && !it.name.contains('archunit-junit') })
    }
}

def configureDependencies = { deps ->
    deps.children().removeIf { dep ->
        dep.scope.text() != 'compile' || !(dep.artifactId.text() in ['archunit'])
    }
}
this.with project(':archunit-junit').configureJUnitArchive(configureDependencies)