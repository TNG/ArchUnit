plugins {
    id 'archunit.java-release-conventions'
}

ext.moduleName = 'com.tngtech.archunit.junit4'

dependencies {
    api project(path: ':archunit', configuration: 'shadow')
    api project(path: ':archunit-junit', configuration: 'archJunitApi')
    api dependency.junit4
    implementation project(path: ':archunit-junit', configuration: 'shadow')
    dependency.addGuava { dependencyNotation, config -> implementation(dependencyNotation, config) }
    implementation dependency.slf4j

    testImplementation dependency.log4j_api
    testImplementation dependency.log4j_core
    testImplementation dependency.log4j_slf4j
    testImplementation dependency.junit4
    testImplementation dependency.junit_dataprovider
    testImplementation dependency.mockito
    testImplementation dependency.assertj
    testImplementation(dependency.assertj_guava) {
        exclude module: 'assertj-core'
        exclude module: 'guava'
    }
    testImplementation project(path: ':archunit', configuration: 'tests')
    testImplementation project(path: ':archunit-junit', configuration: 'tests')

    // This is a hack for running tests with IntelliJ instead of delegating to Gradle,
    // because for some reason this dependency cannot be resolved otherwise only for archunit-junit4 :-(
    testRuntimeOnly dependency.asm
}

javadoc {
    source(['archJunitApi', 'main'].collect { project(':archunit-junit').sourceSets[it].allJava })
}
sourcesJar {
    ['archJunitApi', 'main'].each {
        from project(':archunit-junit').sourceSets[it].allSource
    }
}

configureSlowTestsFor this

shadowJar {
    exclude 'META-INF/**'

    dependencies {
        exclude(project(':archunit'))
        exclude(dependency(dependency.junit4))
        exclude(dependency(dependency.slf4j))
        exclude(dependency(dependency.hamcrest)) // Shadow doesn't respect transitive excludes :-(
    }
}

def configureDependencies = { deps ->
    deps.children().removeIf { dep ->
        dep.scope.text() != 'compile' || !(dep.artifactId.text() in ['archunit', 'archunit-junit5-api', 'archunit-junit5-engine-api', 'junit'])
    }
}
this.with project(':archunit-junit').configureJUnitArchive(configureDependencies)
